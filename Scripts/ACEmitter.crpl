#This is my custom AC emitter script. It behaves pretty similarly to a standard AC emitter. It has custom popup text to match my custom creeper emitter, and functionality to be improved with my ACmultitower.crpl script.
$Name:"ACEmitter"
#ImageSlot can be changed if you want to use a different slot.
$ImageSlot:"Custom1"
$ACamt:-10 #Amount of AC emitted baseline
$ACinterval:15 #Frames between emissions
$StartDelay:0 #Delays that many frames before startingg production.
$Unstopped:1 #Do not alter. If set to 0 prevents emitter from working.
$EmpowermentAC:1
once 
	@awake
	#The 4 delay is to make sure the AC multi tower has time to establish its variables before being picked up by this script. My custom creeper emitter script also has this delay to avoid unbalance.
	4 delay
endonce 
 
GetGameTimeFrames <-StartDelay gt if

	 
	#If we've flagged a multi tower, we have to check on every invocation to update our ACamt in case the global multi has changed since our last check. Otherwise the total ACamt remains at the value we set back in the initial check.
	<-multi if
		<-ACamt <-TotalPower <-EmpowermentAC mul mul <-EmissionBonus sub  <-Unstopped mul ->tAmount
	else
		<-ACamt <-EmpowermentAC mul <-EmissionBonus sub <-Unstopped mul ->tAmount
	endif
	<-tAmount neq0 if
		CurrentCoords <-tAmount SetCreeperNoLower
	endif
	"Producing " <-tAmount <-Unstopped mul abs concat " anti-creeper every " concat <-ACinterval 30.00 div 2 Round 1000 mul concat " milliseconds." concat "


	" concat SetPopupText
	<-ACinterval Delay
else
	<-StartDelay GetGameTimeFrames sub 30.0 div ceil ->rem
	"This emitter is currently slumbering. It will awake in " <-rem " seconds with an output of " <-ACamt neg ".
	
	
	"
	concat concat concat concat SetPopupText
	15 Delay
endif



:awake
	Self CONST_COUNTSFORVICTORY 0 SetUnitAttribute Self CONST_NULLIFIERDAMAGES 0 SetUnitAttribute Self CONST_SUPPORTSDIGITALIS 0 SetUnitAttribute
	Self CONST_CREATEPZ 0 SetUnitAttribute
	Self "main" <-ImageSlot SetImage
	Self "main" 0.8 0.8 SetImageScale
	Self "main" 255 255 255 255 SetImageColor
	Self "EmitterInterface.crpl" "HasEmitterInterface" GetScriptVar eq0 if
		Self "EmitterInterface.crpl" AddScriptToUnit
	endif
	Self "EmitterInterface.crpl" "EmitterType" <-Name SetScriptVar
	Self "EmitterInterface.crpl" "EmitterNetName" 0 SetScriptVar