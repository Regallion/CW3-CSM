#DISCO

#Creeper amount and anticreeper amount are stored in separate variables so you can have it produce different amounts of each.
$Camt:50
$ACamt:-10
#Timers are also stored separately.
$ACinterval:150
$Cinterval:150
$Range:5
#StartState of 1 means the emitter will produce creeper initially. Value of 0 will create AC.
$Name:"DiscoEmitter"
#Thresh is how much creeper/AC must flood the emitter in order for it to flip.
#ImageSlot can be changed if you want to use a different slot.
$ImageSlot:"Custom0"
$Empowerment:1
$EmpowermentAC:1
$Unstopped:1
Once
1 ->Cvar
1 ->ACvar
@awake
#The delay is to match the other emitters.
4 delay
<-ACamt ->tAmount
1 ->Creepin
CurrentCoords ->Y ->X 
endonce
GetTimer0 eq0 <-Creepin and if
	@CProd
	Self "main" 0 256 RandInt 0 256 RandInt 0 256 RandInt 125 256 RandInt SetImageColor
	0 ->Creepin
	1 ->Acreepin
	<-ACinterval SetTimer1
endif
GetTimer1 eq0 <-Acreepin and if
	<-multi if
		<-ACamt <-TotalPower <-EmpowermentAC mul mul  <-ACvar mul <-EmissionBonus sub  <-Unstopped mul ->tAmount
	else
		<-ACamt <-EmpowermentAC mul <-ACvar mul <-EmissionBonus sub   <-Unstopped mul ->tAmount
	endif
	@ACProd
	Self "main" 0 256 RandInt 0 256 RandInt 0 256 RandInt 125 256 RandInt SetImageColor
	1 ->Creepin
	0 ->Acreepin
	<-Cinterval SetTimer0
endif
:awake
	Self CONST_COUNTSFORVICTORY 0 SetUnitAttribute Self CONST_NULLIFIERDAMAGES 0 SetUnitAttribute Self CONST_SUPPORTSDIGITALIS 0 SetUnitAttribute
	Self "main" <-ImageSlot SetImage
	Self "main" 0.8 0.8 SetImageScale
	"DISCO" SetPopupText
	Self "EmitterInterface.crpl" "HasEmitterInterface" GetScriptVar eq0 if
		Self "EmitterInterface.crpl" AddScriptToUnit
	endif
	Self "EmitterInterface.crpl" "EmitterType" <-Name SetScriptVar
	Self "EmitterInterface.crpl" "EmitterNetName" 0 SetScriptVar

#Creeper production function
:CProd
	<-Y <-Range 1 add add <-Y <-Range sub do
		<-X <-Range 1 add add <-X <-Range sub do
			I J GetCreeper 1500 lt if
				I J <-Camt <-Empowerment <-NegaPower add 0.1 max mul <-Cvar mul <-Unstopped mul AddCreeper
			endif
		loop
	loop
#AC production function.
:ACProd
	#Only update the tAmount value if multi has been flagged. Otherwise we stick with the base value.
	<-Y <-Range 1 add add <-Y <-Range sub do
		<-X <-Range 1 add add <-X <-Range sub do
			 I J GetCreeper -1500 gt if
				I J <-tAmount <-Unstopped mul AddCreeper
			endif
		loop
	loop